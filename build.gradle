import java.nio.file.Paths
import java.nio.file.Files

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

evaluationDependsOn(":ZeroCore2-1.20")

file('build.properties').withReader {

    def properties = new Properties()
	
    properties.load(it)
    project.ext.config = new ConfigSlurper().parse properties
}

version = "${config.minecraft_version}-${config.mod_version_major}.${config.mod_version_minor}.${config.mod_version_patch}"
group = 'it.zerono.mods.extremereactors'
archivesBaseName = 'ExtremeReactors2'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {

    mappings channel: 'official', version: config.minecraft_mappings

    def runPath = Paths.get(System.getenv('mc_runs'), "forge")

    if (!Files.exists(runPath)) {
        runPath = Paths.get("${rootProject.projectDir}", "runs")

        if (!Files.exists(runPath)) {
            Files.createDirectories(runPath)
        }
    }

    def clientRunDir = runPath.resolve("client-${config.minecraft_version}").toFile();
    def serverRunDir = runPath.resolve("server-${config.minecraft_version}").toFile();

    if (!clientRunDir.exists()) {
        clientRunDir.mkdirs();
    }

    if (!serverRunDir.exists()) {
        serverRunDir.mkdirs();
    }

    runs {
        client {

            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"

            workingDirectory clientRunDir

            ideaModule "${rootProject.name}.${project.name}.main".replace(' ', '_')

            mods {
                zerocore {
                    source project(':ZeroCore2-1.20').sourceSets.main
                }

                bigreactors {
                    source sourceSets.main
                }

                bigreactorstest {
                    source sourceSets.test
                }
            }
        }

        server {

            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            
			workingDirectory serverRunDir

            ideaModule "${rootProject.name}.${project.name}.main".replace(' ', '_')

            mods {
                zerocore {
                    source project(':ZeroCore2-1.20').sourceSets.main
                }

                bigreactors {
                    source sourceSets.main
                }

                bigreactorstest {
                    source sourceSets.test
                }
            }
        }

        data {

            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"

            workingDirectory clientRunDir

            ideaModule "${rootProject.name}.${project.name}.main".replace(' ', '_')

            args '--mod', 'bigreactors', '--all',
                    '--existing', file('src/main/resources/'),
                    '--existing', file('src/generated/resources/'),
                    '--output', file('src/generated/resources/')

            mods {
                zerocore {
                    source project(':ZeroCore2-1.20').sourceSets.main
                }

                bigreactors {
                    source sourceSets.main
                }

                bigreactorstest {
                    source sourceSets.test
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

processResources {

	inputs.property "mod_version", version
	inputs.property "mod_name", config.mod_name
    inputs.property "minecraft_versionrange", config.minecraft_versionrange
	inputs.property "forge_versionrange", config.forge_versionrange
    inputs.property "loader_versionrange", config.loader_versionrange
	inputs.property "zerocore_versionrange", config.zerocore_versionrange

    from(sourceSets.main.resources.srcDirs) {
	
        include 'ExtremeReactors2-1.20/src/main/resources/META-INF/mods.toml'
        expand 'mod_version': version, 
				'mod_name': config.mod_name,
				'minecraft_versionrange': config.minecraft_versionrange,
				'forge_versionrange': config.forge_versionrange,
                'loader_versionrange': config.loader_versionrange,
				'zerocore_versionrange': config.zerocore_versionrange
    }
}

repositories {

    // jei
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }

    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }

    // cc-tweaked
    maven {
        url 'https://squiddev.cc/maven/'
    }

    // patchouli
    maven {
        url 'https://maven.blamejared.com'
    }

    // cursemaven
    maven {
        url 'https://www.cursemaven.com'
    }
}

dependencies {
	minecraft "net.minecraftforge:forge:${config.minecraft_version}-${config.forge_version}"
    implementation project(':ZeroCore2-1.20')

    // JEI
    compileOnly(fg.deobf("mezz.jei:jei-${config.minecraft_version}-common-api:${config.jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${config.minecraft_version}-forge-api:${config.jei_version}"))
    runtimeOnly(fg.deobf("mezz.jei:jei-${config.minecraft_version}-forge:${config.jei_version}"))

    // cc-tweaked
    compileOnly("cc.tweaked:cc-tweaked-${config.minecraft_version}-core-api:${config.cctweaked_version}")
    compileOnly(fg.deobf("cc.tweaked:cc-tweaked-${config.minecraft_version}-forge-api:${config.cctweaked_version}"))
    runtimeOnly(fg.deobf("cc.tweaked:cc-tweaked-${config.minecraft_version}-forge:${config.cctweaked_version}"))

	// patchouli
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${config.patchouli_version}:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${config.patchouli_version}")

    // mekanism
//    compileOnly "mekanism:Mekanism:${config.mekanism_version}:api"
//    runtimeOnly fg.deobf("mekanism:Mekanism:${config.mekanism_version}")
//    runtimeOnly fg.deobf("mekanism:Mekanism:${config.mekanism_version}:additions")
//    runtimeOnly fg.deobf("mekanism:Mekanism:${config.mekanism_version}:generators")
//    runtimeOnly fg.deobf("mekanism:Mekanism:${config.mekanism_version}:tools")

    // thermal
//    runtimeOnly fg.deobf("curse.maven:thermalexp-69163:4112130")
//    runtimeOnly fg.deobf("curse.maven:thermalfnd-222880:4112137")
//    runtimeOnly fg.deobf("curse.maven:cofhcore-69162:4202944")
//    runtimeOnly fg.deobf("curse.maven:redarsenal-70631:4121288")

	// constructionwand
//    implementation fg.deobf("curse.maven:constructionwand-399558:4455720")
}

task extremereactors_apiJar(type: Jar) {

    from(sourceSets.main.allJava) {
        include 'it/zerono/mods/extremereactors/api/**'
    }
    from (sourceSets.main.output) {
        include 'it/zerono/mods/extremereactors/api/**'
    }

    archiveClassifier = 'api'
}

jar {
    manifest {
        attributes 'Specification-Title': config.mod_name
        attributes 'Specification-Vendor': 'ZeroNoRyouki'
        attributes 'Specification-Version': "2"
        attributes 'Implementation-Title': config.mod_name
        attributes 'Implementation-Vendor': 'ZeroNoRyouki'
        attributes 'Implementation-Version': version
        attributes 'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }

    finalizedBy('reobfJar')
}

publish.dependsOn('reobfJar')

publishing {

    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            artifact jar
            artifact extremereactors_apiJar
        }
    }

    repositories {
        maven {
            url 'file://' + System.getenv('mc_maven')
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
